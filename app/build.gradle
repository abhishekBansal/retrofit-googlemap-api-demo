apply plugin: 'com.android.application'

// auto increment version stuff
import abhishek.demo.retrofit.googlemapapi.MBIncrementVersionTask

task incrementVersionTask(type: MBIncrementVersionTask)
tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'incrementVersionTask'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    final API_URL_DEV = "http://maps.googleapis.com/maps/api/"

    defaultConfig {
        applicationId "abhishek.demo.retrofit.googlemapapi"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode incrementVersionTask.getCurrentVersionCode()
        versionName incrementVersionTask.getCurrentVersionName()

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // setting custom name for apk
        project.ext.set("archivesBaseName", "AndroidJumpstart-" + defaultConfig.versionName
                + "-vc" + defaultConfig.versionCode)
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField 'String', 'API_URL', "\"" + API_URL_DEV + "\""
        }

        release {
            buildConfigField 'String', 'API_URL', "\"" + API_URL_DEV + "\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    // Use Retrofit for API interaction
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    // Lombok
    // https://projectlombok.org/setup/android.html
    compileOnly 'org.projectlombok:lombok:1.16.18'
    annotationProcessor "org.projectlombok:lombok:1.16.18"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Timber for logging
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Butterknife for view injection
    implementation 'com.jakewharton:butterknife:8.8.1'
}